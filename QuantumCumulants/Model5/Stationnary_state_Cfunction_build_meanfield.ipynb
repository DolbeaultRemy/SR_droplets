{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 43,
   "id": "331aab86-486f-4179-b123-9d928049e906",
   "metadata": {},
   "outputs": [],
   "source": [
    "using QuantumCumulants\n",
    "using Symbolics\n",
    "using JLD2"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 44,
   "id": "384a2ff4",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "change_mkfile"
      ]
     },
     "execution_count": 44,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "\"\"\" Function that writes the Cfunctions, used to be distributed between all the threads \"\"\"\n",
    "function write_c_func(eqs, chunk, var_array, p0)\n",
    "    for i in chunk\n",
    "        code = Symbolics.build_function([eqs[i].rhs], var_array, target=Symbolics.CTarget())\n",
    "        c_code = replace(code, \n",
    "            \"im\" => \"*I\", \"double* du\" => \"double complex* du\", \"const double* RHS1\" => \"double complex* RHS1, const double complex* parameters\", \"du[0]\" => \"du[$(i-1)]\", \"diffeqf\" => \"diffeqf_$i\")\n",
    "        # Replace the parameters by a parametor vector\n",
    "        c_code = replace(c_code, p0...)\n",
    "        open(\"Cfunctions/diffeqf_$i.c\", \"w\") do io\n",
    "            print(io, \"#include <complex.h>\\n\") # Include complex package\n",
    "            print(io, c_code)\n",
    "        end\n",
    "    end\n",
    "end\n",
    "\n",
    "\"\"\" Function creating the makefile for the corresponding HT frequency \"\"\"\n",
    "function change_mkfile(N)\n",
    "    write(\"Makefile\", replace(read(\"MakefileTemplate.txt\", String), \"liballfuncs.dll\"=>\"libs/liballfuncs_$(N)_meanfield.dll\"))\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 45,
   "id": "1af0dc51",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "5"
      ]
     },
     "execution_count": 45,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "Threads.nthreads()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 46,
   "id": "7cff54ea",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Nbr of atoms\n",
    "N = 10;"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "4a92b0ab-9503-4642-b860-37abbec82e60",
   "metadata": {},
   "source": [
    "# Derivation of the symbolic MPC equations"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 47,
   "id": "3f0bb988-0c7c-417e-9fed-96dca6b54d95",
   "metadata": {},
   "outputs": [],
   "source": [
    "@cnumbers Nsymb ΩROs\n",
    "\n",
    "h = NLevelSpace(Symbol(:atom),2)\n",
    "\n",
    "exp_RO(i) = IndexedVariable(:expRO, i)\n",
    "conj_exp_RO(i) = IndexedVariable(:conjexpRO, i)\n",
    "Γ(i,j) = IndexedVariable(:Γ,i,j)\n",
    "Ω(i,j) = IndexedVariable(:Ω,i,j;identical=false)\n",
    "\n",
    "i = Index(h,:i,Nsymb,h)\n",
    "j = Index(h,:j,Nsymb,h)\n",
    "k = Index(h,:k,Nsymb,h)\n",
    "\n",
    "σ(x,y,z) = IndexedOperator(Transition(h,:σ,x,y),z)\n",
    "\n",
    "H_RO = ΩROs * ∑(exp_RO(i)*σ(2,1,i) + conj_exp_RO(i)*σ(1,2,i), i)\n",
    "H_elec = Σ(Ω(i,j)*σ(2,1,i)*σ(1,2,j), j, i)\n",
    "\n",
    "H = Symbolics.simplify(H_RO + H_elec)\n",
    "\n",
    "J = [σ(1,2,i)] # σ-, jump operators for the Lindbladian\n",
    "rates = [Γ(i,j)]\n",
    "\n",
    "ops = [σ(2, 2, k), σ(2, 1, k)]; # n_up/σ+"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 48,
   "id": "02903b99-699c-48ad-b957-f311223bbabf",
   "metadata": {},
   "outputs": [],
   "source": [
    "eqs = meanfield(ops,H,J;rates=rates,order=1)\n",
    "complete!(eqs);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 49,
   "id": "e28c8a33-61f9-4be6-95f2-1e816bb883ac",
   "metadata": {},
   "outputs": [],
   "source": [
    "eqs_eval = QuantumCumulants.evaluate(eqs; limits=(Nsymb=>N));"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "7220e65d",
   "metadata": {},
   "source": [
    "### Compute variables"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 50,
   "id": "c0fcccd1",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Create directories\n",
    "if !isdir(\"Cfunctions\")\n",
    "    mkdir(\"Cfunctions\")\n",
    "end\n",
    "if !isdir(\"libs\")\n",
    "    mkdir(\"libs\")\n",
    "end\n",
    "if !isdir(\"op_list\")\n",
    "    mkdir(\"op_list\")\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 51,
   "id": "031435f3",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Compute variables\n",
    "op_list = []\n",
    "var_array = []\n",
    "for i in 1:length(eqs_eval)\n",
    "    var = eqs_eval[i].lhs\n",
    "    push!(var_array, var)\n",
    "    \n",
    "    v_str = string(var)\n",
    "    em = eachmatch(r\"σ(\\d+)\", v_str)\n",
    "    ind = [m.captures[1] for m in em]\n",
    "    push!(op_list, [parse(Int, i) for i in ind])\n",
    "end\n",
    "\n",
    "@save \"op_list/op_list_$(N)_meanfield.jdl2\" op_list"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "daaa3cda",
   "metadata": {},
   "source": [
    "### Build function"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 52,
   "id": "84a3b513",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Replace parameters by a big vector\n",
    "Γij_symb = [Γ(i,j) for i = 1:N for j=1:N]\n",
    "Ωij_symb = [Ω(i,j) for i = 1:N for j=1:N if i≠j]\n",
    "exp_RO_symb = [exp_RO(i) for i = N:-1:1] # We go in the decreasing direction to avoid exp_RO(10) being replace by exp_RO(1)0\n",
    "conj_exp_RO_symb = [conj_exp_RO(i) for i = N:-1:1]\n",
    "ps = [[string(Γ) for Γ in Γij_symb]; [string(Ω) for Ω in Ωij_symb]; [string(exp_RO) for exp_RO in exp_RO_symb]; [string(conj_exp_RO) for conj_exp_RO in conj_exp_RO_symb]; string(ΩROs)]\n",
    "\n",
    "p0 = [\"parameters[$(i-1)]\" for i = 1:length(ps)]\n",
    "p0 = ps .=> p0;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 53,
   "id": "01166805",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Build Cfunctions\n",
    "list_eqs = 1:length(eqs_eval)\n",
    "chunks = Iterators.partition(list_eqs, cld(length(list_eqs), Threads.nthreads()))\n",
    "tasks = map(chunks) do chunk # Split the equations in chunck solved on each core\n",
    "    Threads.@spawn write_c_func(eqs_eval, chunk, var_array, p0)\n",
    "end\n",
    "wait.(tasks);"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "67a66f27",
   "metadata": {},
   "source": [
    "### Build dispatcher"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 54,
   "id": "ad3ce508",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\" Create the big function that will call all the subfunctions to avoid doing a lot of ccalls \"\"\"\n",
    "function generate_full_dispatcher(filename::String, n::Int)\n",
    "    open(filename, \"w\") do io\n",
    "        println(io, \"#include <complex.h>\\n#include <math.h>\\n\")\n",
    "        # External subfunctions\n",
    "        for i in 1:(n)\n",
    "            println(io, \"extern void diffeqf_$i(double complex* du, double complex* RHS1, const double complex* parameters);\")\n",
    "        end\n",
    "\n",
    "        println(io, \"\\nvoid diffeqf(double complex* du, double complex* RHS1, const double complex* parameters) {\")\n",
    "\n",
    "        # Call the subfunctions\n",
    "        for i in 1:(n)\n",
    "            println(io, \"    diffeqf_$i(du, RHS1, parameters);\")\n",
    "        end\n",
    "\n",
    "        println(io, \"}\")\n",
    "    end\n",
    "end\n",
    "\n",
    "generate_full_dispatcher(\"dispatcher.c\", length(eqs_eval))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "8dc617e7",
   "metadata": {},
   "source": [
    "### Build object file"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 55,
   "id": "d1163b4a",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\" Create the object file (with the name of all the functions and their corresponding files) to avoid compilation issue \"\"\"\n",
    "function objs_file()\n",
    "    open(\"objs.txt\", \"w\") do io\n",
    "         println(io, \"dispatcher.o\")\n",
    "        for i in 1:length(eqs_eval)\n",
    "            println(io, \"Cfunctions/diffeqf_$i.o\")\n",
    "        end\n",
    "    end\n",
    "end\n",
    "\n",
    "objs_file()"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "6f026d47",
   "metadata": {},
   "source": [
    "### Change the makefile"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 56,
   "id": "546c0654",
   "metadata": {},
   "outputs": [],
   "source": [
    "change_mkfile(N);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "4d19c021",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia (5 threads) 1.12.1",
   "language": "julia",
   "name": "julia-_5-threads_-1.12"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
