{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "8e133098",
   "metadata": {},
   "outputs": [],
   "source": [
    "import PhysicalConstants.CODATA2018: c_0\n",
    "using QuantumCumulants\n",
    "using CollectiveSpins\n",
    "using Symbolics\n",
    "using JLD2\n",
    "using Unitful"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e64a65bd",
   "metadata": {},
   "source": [
    "# System"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "4ae170bc",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Nbr of particles\n",
    "N = 2\n",
    "\n",
    "# Normalisation parameters\n",
    "λ = 421e-9\n",
    "γ = 32.7e6 # In Hz\n",
    "\n",
    "# Physical values\n",
    "ω0 = (2π*ustrip(c_0)/λ)\n",
    "ωl = ω0\n",
    "kl = [ustrip(c_0)/ωl, 0, 0] # Laser along x\n",
    "Ω_RO = 1e7 # Taken from Barbut arXiv:2412.02541v1\n",
    "\n",
    "# Position of atoms\n",
    "Lx, Ly, Lz = [1, 1, 1] * 1e-6\n",
    "r0 = [[rand(Float64)*Lx, rand(Float64)*Ly, rand(Float64)*Lz] for i in 1:N]\n",
    "\n",
    "# Save the atoms position for comparison with QuantumOptics\n",
    "@save \"r0.jdl2\" r0\n",
    "\n",
    "# Normalization\n",
    "ω0 = ω0 / γ\n",
    "ωl = ωl / γ\n",
    "kl = kl * λ\n",
    "r0 = r0 / λ\n",
    "Ω_RO = Ω_RO / γ\n",
    "\n",
    "# Compute the Ω and Γ matrices of the electric dipole-dipole interaction using CollectiveSpins\n",
    "e = [0, 0, 1.] # Quantization axis along z\n",
    "system = SpinCollection(r0, e, gammas=1.)\n",
    "Ω_CS = interaction.OmegaMatrix(system)\n",
    "Γ_CS = interaction.GammaMatrix(system)\n",
    "\n",
    "# Integration parameter\n",
    "tstep = 0.1\n",
    "T = [0:tstep:10;]; # Normalised time"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "0b4994a8",
   "metadata": {},
   "source": [
    "# Derivation of the symbolic MPC equations"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "db680f33",
   "metadata": {},
   "outputs": [],
   "source": [
    "@cnumbers Nsymb ΩROs\n",
    "\n",
    "h = NLevelSpace(Symbol(:atom),2)\n",
    "\n",
    "exp_RO(i) = IndexedVariable(:expRO, i)\n",
    "conj_exp_RO(i) = IndexedVariable(:conjexpRO, i)\n",
    "Γ(i,j) = IndexedVariable(:Γ,i,j)\n",
    "Ω(i,j) = IndexedVariable(:Ω,i,j;identical=false)\n",
    "\n",
    "i = Index(h,:i,Nsymb,h)\n",
    "j = Index(h,:j,Nsymb,h)\n",
    "k = Index(h,:k,Nsymb,h)\n",
    "\n",
    "σ(x,y,z) = IndexedOperator(Transition(h,:σ,x,y),z)\n",
    "\n",
    "H_RO = ΩROs * ∑(exp_RO(i)*σ(2,1,i) + conj_exp_RO(i)*σ(1,2,i), i)\n",
    "H_elec = Σ(Ω(i,j)*σ(2,1,i)*σ(1,2,j), j, i)\n",
    "\n",
    "H = Symbolics.simplify(H_RO + H_elec)\n",
    "\n",
    "J = [σ(1,2,i)] # σ-, jump operators for the Lindbladian\n",
    "rates = [Γ(i,j)]\n",
    "\n",
    "ops = [σ(2, 2, k), σ(2, 1, k)]; # n_up/σ+"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "b2e4fd41",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "(Σ(j=1:Nsymb)(j≠i)Σ(i=1:Nsymb)(i≠j)Ωij*(σ21i*σ12j)+Σ(i=1:Nsymb)expROi*ΩROs*(σ21i)+Σ(i=1:Nsymb)conjexpROi*ΩROs*(σ12i))"
     ]
    }
   ],
   "source": [
    "print(H)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "7c0321c4",
   "metadata": {},
   "outputs": [],
   "source": [
    "eqs = meanfield(ops,H,J;rates=rates,order=2)\n",
    "complete!(eqs);"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "aa00fe25-3f1c-4f16-a80c-bd0b3383cccc",
   "metadata": {},
   "source": [
    "### Evaluate equations"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "97d219e6-0634-4348-8032-3648c24aa7b9",
   "metadata": {},
   "outputs": [],
   "source": [
    "eqs_eval = evaluate(eqs; limits=(Nsymb=>N));"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "83ab15ad-2fd8-4640-9dfa-6877d392f244",
   "metadata": {},
   "source": [
    "### Evaluate parameters"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "id": "10366457-7721-4ea4-b1d7-7e0e379ab1f1",
   "metadata": {},
   "outputs": [],
   "source": [
    "Γij_symb = [Γ(i,j) for i = 1:N for j=1:N]\n",
    "Ωij_symb = [Ω(i,j) for i = 1:N for j=1:N if i≠j]\n",
    "\n",
    "Γij_ = [Γ_CS[i, j] for i = 1:N for j=1:N]\n",
    "Ωij_ = [Ω_CS[i, j] for i = 1:N for j=1:N if i≠j]\n",
    "\n",
    "exp_RO_symb = [exp_RO(i) for i = 1:N]\n",
    "conj_exp_RO_symb = [conj_exp_RO(i) for i = 1:N]\n",
    "\n",
    "exp_RO_ = [exp(1im*r0[i]'kl) for i =1:N]\n",
    "conj_exp_RO_ = [exp(-1im*r0[i]'kl) for i =1:N]\n",
    "\n",
    "\n",
    "\n",
    "ps = [Γij_symb; Ωij_symb; exp_RO_symb; conj_exp_RO_symb; ΩROs]\n",
    "p0 = [Γij_; Ωij_; exp_RO_; conj_exp_RO_; Ω_RO/2]\n",
    "\n",
    "p0 = ps .=> p0;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "id": "c9e260a8-4d6c-4cca-87a7-cc7a14c99e6a",
   "metadata": {},
   "outputs": [],
   "source": [
    "eqs_eval = substitute(eqs_eval, Dict(p0));"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c6ec1957-4cc6-4f79-ab07-9ec8e0740753",
   "metadata": {},
   "source": [
    "### Compute variables"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "id": "dd2e24bb-d065-46be-9f3a-ffb335a85ca6",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Save the expectation values of the operators which differential equation has to be solved\n",
    "op_list = []\n",
    "var_array = []\n",
    "for i in 1:length(eqs_eval)\n",
    "    var = eqs_eval[i].lhs\n",
    "    push!(var_array, var)\n",
    "    \n",
    "    v_str = string(var)\n",
    "    em = eachmatch(r\"σ(\\d+)\", v_str)\n",
    "    ind = [m.captures[1] for m in em]\n",
    "    push!(op_list, [parse(Int, i) for i in ind])\n",
    "end\n",
    "@save \"op_list.jdl2\" op_list"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "04465c5d-6a58-4195-8753-c64e373778bc",
   "metadata": {},
   "source": [
    "### Build function"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "id": "fdec997c-8f45-4724-aa94-b8428171e6be",
   "metadata": {},
   "outputs": [],
   "source": [
    "if !isdir(\"Cfunctions\")\n",
    "    mkdir(\"Cfunctions\")\n",
    "end\n",
    "\n",
    "\n",
    "for i in 1:length(eqs_eval)\n",
    "    # Save the C function\n",
    "    code = Symbolics.build_function([eqs_eval[i].rhs], var_array, target=Symbolics.CTarget())\n",
    "    c_code = replace(code, \n",
    "        \"im\" => \"*I\", \"double* du\" => \"double complex* du\", \"const double* RHS1\" => \"const double complex* RHS1\", \"du[0]\" => \"du[$(i-1)]\", \"diffeqf\" => \"diffeqf_$i\")\n",
    "    open(\"Cfunctions/diffeqf_$i.c\", \"w\") do io\n",
    "        print(io, \"#include <complex.h>\\n\") # Include complex package\n",
    "        print(io, c_code)\n",
    "    end\n",
    "    # Free RAM memomry\n",
    "    code = nothing\n",
    "    c_code = nothing\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2987d38c-ff88-40c9-9b94-660a76176937",
   "metadata": {},
   "source": [
    "### Build dispatcher"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 11,
   "id": "5f6c9bdb-df05-4ef5-81c3-2cdf81b621d3",
   "metadata": {
    "jupyter": {
     "source_hidden": true
    }
   },
   "outputs": [],
   "source": [
    "\"\"\" Create the big function that will call all the subfunctions to avoid doing a lot of ccalls \"\"\"\n",
    "function generate_full_dispatcher(filename::String, n::Int)\n",
    "    open(filename, \"w\") do io\n",
    "        println(io, \"#include <complex.h>\\n#include <math.h>\\n\")\n",
    "        # External subfunctions\n",
    "        for i in 1:(n)\n",
    "            println(io, \"extern void diffeqf_$i(double complex* du, const double complex* RHS1);\")\n",
    "        end\n",
    "\n",
    "        println(io, \"\\nvoid diffeqf(double complex* du, const double complex* RHS1) {\")\n",
    "\n",
    "        # Call the subfunctions\n",
    "        for i in 1:(n)\n",
    "            println(io, \"    diffeqf_$i(du, RHS1);\")\n",
    "        end\n",
    "\n",
    "        println(io, \"}\")\n",
    "    end\n",
    "end\n",
    "\n",
    "generate_full_dispatcher(\"dispatcher.c\", length(eqs_eval))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c1ffc90c-33d5-402f-893d-bb3daaeedb1a",
   "metadata": {},
   "source": [
    "### Build object file"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 12,
   "id": "00381bc6-0d05-4f18-a683-d8c75085e771",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\" Create the object file (with the name of all the functions and their corresponding files) to avoid compilation issue \"\"\"\n",
    "function objs_file()\n",
    "    open(\"objs.txt\", \"w\") do io\n",
    "         println(io, \"dispatcher.o\")\n",
    "        for i in 1:length(eqs_eval)\n",
    "            println(io, \"Cfunctions/diffeqf_$i.o\")\n",
    "        end\n",
    "    end\n",
    "end\n",
    "\n",
    "objs_file()"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.12.1",
   "language": "julia",
   "name": "julia-1.12"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
